addressLength =
    addressLength3
  | addressLength2
  | addressLength1

addressLength1 =
    REAL CM
  | REAL
  | FREEVARIABLE
  | previousLength
  | LENGTH addressLineSegment
  | addressLineSegment

addressLength2 =
    GREATERTHAN addressLength1
  | LESSTHAN addressLength1
  | TWICE addressLength1
  | THRICE addressLength1
  | REAL TIMES addressLength1
  | HALF addressLength1
  | operation addressLength1 addressLength1

addressLength3 =
    GREATERTHAN addressLength2
  | LESSTHAN addressLength2
  | TWICE addressLength2
  | THRICE addressLength2
  | REAL TIMES addressLength2
  | HALF addressLength2
  | operation addressLength2 addressLength2  

addressDegree =
    addressDegree3
  | addressDegree2
  | addressDegree1

addressDegree1 =
    REAL DEGREES
  | REAL
  | FREEVARIABLE
  | previousDegree
  | addressAngle

addressDegree2 =
    GREATERTHAN addressDegree1
  | LESSTHAN addressDegree1
  | TWICE addressDegree1
  | THRICE addressDegree1
  | REAL TIMES addressDegree1
  | HALF addressDegree1
  | operation addressDegree1 addressDegree1

addressDegree3 =
    GREATERTHAN addressDegree2
  | LESSTHAN addressDegree2
  | TWICE addressDegree2
  | THRICE addressDegree2
  | REAL TIMES addressDegree2
  | HALF addressDegree2
  | operation addressDegree2 addressDegree2  

TIMES =
    'times'

HALF =
    'half'

GREATERTHAN =
    'greater' 'than'

LESSTHAN =
    'less' 'than'

commands = 
    commands command
  | command

command =
    constructCommand 
  | markCommand 
  | cutCommand
  | joinCommand
  | divideCommand

constructCommand = 
  CONSTRUCT constructibleAndProperties

CONSTRUCT =
    'construct'
  | 'draw'

constructibleAndProperties = 
    lineSegmentAndProperties
  | lineAndProperties
  | arcAndProperties
  | circleAndProperties
  | angleAndProperties

lineSegmentAndProperties = 
    addressLineSegment GIVENTHAT conditions
  | addressLineSegment lineSegmentProperties

conditions =
    condition
  | condition condition
  | condition condition condition
  | condition condition condition condition

condition =
    LENGTH addressLineSegment EQUALS addressLength
  | addressLineSegment EQUALS addressLength
  | addressAngle EQUALS addressAngle

GIVENTHAT =
    'given' 'that'
  | 'given'

LINESEGMENT =
  'line' 'segment'

POINTPAIR =
  [A-Z][A-Z]

lineSegmentProperties = 
    LENGTH addressLength
  | addressLength

LENGTH =
  'length'

CM =
  'cm'

addressLineSegment =
    LINESEGMENT POINTPAIR
  | POINTPAIR
  | adjectivePrevious LINESEGMENT
  | addressFreeObject

angleAndProperties =
    ANGLE VERTEX addressPoint addressDegree
  | ANGLE addressAngle addressDegree

VERTEX =
    'vertex'

ANGLE =
    'angle'

addressAngle =
    ANGLE POINTTRIPLET
  | ANGLE POINTSINGLET
  | POINTTRIPLET
  | POINTSINGLET
  | adjectivePrevious ANGLE

DEGREES =
    'degree'
  | 'degrees'

circleAndProperties = 
    CIRCLE CENTER addressPoint RADIUS addressLength
  | CIRCLE CENTER addressPoint DIAMETER addressLength
  | CIRCLE RADIUS addressLength
  | CIRCLE DIAMETER addressLength

CIRCLE =
  'circle'

LINE =
    'line'

lineAndProperties = 
    LINE [a-z] 

arcAndProperties =
  ARC arcProperties

ARC =
    'arc'
  | 'arcs'

arcProperties =
    centersClause radiiClause mutualIntersectionClause
  | centerClause radiusClause intersectionClause
  | centerClause intersectionClause

mutualIntersectionClause =
    INTERSECTING EACHOTHER AT POINTSINGLET POINTSINGLET
  | INTERSECTING AT POINTSINGLET POINTSINGLET
  | INTERSECTING EACHOTHER AT POINTSINGLET
  | INTERSECTING AT POINTSINGLET

centerClause =
    CENTER POINTSINGLET

centersClause =
    CENTERS POINTSINGLET POINTSINGLET

radiusClause =
    RADIUS addressLength

radiiClause =
    RADIUS addressLength addressLength

intersectionClause =
    INTERSECTING addressIntersectableObject AT POINTSINGLET POINTSINGLET
  | INTERSECTING addressIntersectableObject AT POINTSINGLET

addressIntersectableObject =
    addressLineSegment
  | addressLine
  | addressArc
  | addressCircle
  | addressAngleRays

addressAngleRays =
    RAYS ANGLE addressAngle

RAYS =
    'rays'

CENTER =
  'center'

POINTSINGLET =
  [A-Z]

RADIUS =
  'radius'

DIAMETER =
  'diameter'

REAL =
  [0-9]+(.[0-9]+)?

INTERSECTING =
    'intersecting'
  | 'cutting'
  | 'cut'
  | 'intersect'

EACHOTHER =
    'eachother'
  | 'each' 'other'

CENTERS =
    'centers'

AT =
    'at'

divideCommand =
    DIVIDE divisibleAndProperties

DIVIDE =
    'divide'

divisibleAndProperties =
    divisibleObject INTO [0-9]+ PARTS

PARTS =
  'parts'

INTO =
  'into'

divisibleObject =
    addressLineSegment
  | addressIndefinitePreviousObjects

joinCommand = 
  JOIN addressPointPairs

JOIN =
  'join'

addressPointPairs = 
    POINTPAIR
  | POINTPAIR POINTPAIR
  | POINTPAIR POINTPAIR POINTPAIR
  | adjectivePrevious POINTS

POINTTRIPLET =
  [A-Z][A-Z][A-Z]

markCommand =
    MARK markableAndProperties

MARK = 
    'mark'
  | 'draw'
  | 'label'

markableAndProperties =
    pointAndProperties
  | intersectionPointsAndProperties
  | angleArmPointsAndProperties

angleArmPointsAndProperties =
    POINT POINTSINGLET ON ARM addressAngle POINTSINGLET ON ARM addressAngle

ARM =
  'arm'

intersectionPointsAndProperties = 
    INTERSECTIONPOINTS addressIntersectingObject addressIntersectingObject addressPoint addressPoint
  | INTERSECTIONPOINTS addressIntersectingObject addressIntersectingObject addressPoint
  | INTERSECTIONPOINTS addressIndefinitePreviousObjects addressPoint

INTERSECTIONPOINTS =
    'intersection' 'points'
  | 'intersection' 'point'
  | 'intersection'
  | 'intersections'

addressIntersectingObject = 
    addressArc
  | addressCircle
  | addressLine
  | addressLineSegment
  | addressPreviousObjects

pointAndProperties = 
    POINT POINTSINGLET pointAndPropertiesNotOnCase
  | POINT POINTSINGLET pointAndPropertiesOnCase
  | POINTSINGLET pointAndPropertiesNotOnCase
  | POINTSINGLET pointAndPropertiesOnCase

pointAndPropertiesOnCase =
    ON labelable

pointAndPropertiesNotOnCase =
    NOTON labelable

ON =
  'on'

NOTON =
  'not' 'on'

labelable =
    addressLineSegment
  | addressArc
  | addressLine
  | addressPreviousObjects

addressLine =
    [a-z]
  | LINE [a-z]
  | adjectivePrevious LINE
  | addressFreeObject

addressArc =
  | adjectivePrevious ARC
  | adjectivePrevious ARCS

adjectivePrevious =
    THIS
  | THESE
  | PREVIOUS
  | THOSE

addressPreviousObjects =
    THIS object
  | THESE objects
  | PREVIOUS object
  | PREVIOUS objects
  | THOSE objects
  | addressIndefinitePreviousObjects

addressIndefinitePreviousObjects =
    THIS
  | THESE
  | IT
  | ITS
  | THEM
  | THOSE
  | THEIR

THEIR =
    'their'

ITS =
    'its'

THIS =
  'this'

THESE =
    'these'

PREVIOUS =
  'previous'

IT =
  'it'

THEM =
  'them'

THOSE =
  'those'

object = 
    LINESEGMENT
  | LINE
  | CIRCLE
  | ARC
  | POINT
  | BISECTOR

objects =
    ARCS
  | POINTS
  | BISECTORS
  | object

BISECTOR =
    'bisector'

BISECTORS =
    'bisectors'

ARCS =
    ARC

cutCommand =
  CUT cuttableAndProperties

CUT =
  'cut'

addressPoint =
    POINTSINGLET
  | POINT POINTSINGLET
  | adjectivePrevious POINT
  | addressFreeObject

addressFreeObject =
    'ANY'

ANY =
  'any'

POINT =
    'point'

POINTS =
    'points'

cuttableAndProperties =
    addressLineSegment addressLength fromClause
  | addressLineSegment conditions
  | addressLine atPoints
  | addressArc atPoints
  | addressCircle atPoints

atPoints =
    AT addressPoint
  | AT addressPoint addressPoint

fromClause =
    FROM addressLineSegment
  | FROM addressLine
  | FROM addressPreviousObjects

addressCircle = 
    CIRCLE AT POINTSINGLET
  | CIRCLE CENTER POINTSINGLET
  | adjectivePrevious CIRCLE
  | addressFreeObject  

FROM =
  'from'

TWICE =
  'twice'

THRICE =
  'thrice'

EQUALS =
    'equals'
  | 'equal'
  | 'is'
  | 'has'

previousLength =
    'same'

previousDegree =
    'same'

operation =
    DIFFERENCE
  | SUM

DIFFERENCE =
    'minus'
  | 'difference'

SUM =
    'sum'

FREEVARIABLE =
    'any'
  | 'convenient'
